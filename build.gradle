group = 'com.github.artfable.gradle'
version = '0.0.4'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'artfable.artifact'

buildscript {
    ext {
        kotlin_version = '1.3.20'
        artifact_plugin_version = '0.0.1'
        jakson_version = '2.9.5'
        bintray_version = '1.8.4'

        junit_version = '5.1.0'
        mockito_version = '2.17.0'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://dl.bintray.com/artfable/gradle-plugins'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
        classpath "com.github.artfable.gradle:gradle-artifact-plugin:$artifact_plugin_version"
    }
}


repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jakson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jakson_version"
    compile gradleApi()
    compile localGroovy()

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "org.mockito:mockito-junit-jupiter:$mockito_version"
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://github.com/artfable/gradle-npm-repository-plugin/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "artfable"
            name "Artem Veselov"
            email "art-fable@mail.ru"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'The plugin that allowed load frontend dependencies from package.json')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.ext.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.ext.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-npm-repository-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/artfable/gradle-npm-repository-plugin.git'
        version {
            attributes = ['gradle-plugin': 'artfable.npm:com.github.artfable.gradle:gradle-npm-repository-plugin']
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}